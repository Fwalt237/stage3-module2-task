plugins {
    id 'java-base'
}

ext {
    springVersion = '6.1.5'
    jupiterVersion = '5.10.1'
    archUnitVersion = '1.2.0'
    mapstructVersion = '1.6.0'
    gradlewVersion = '8.5'
}

wrapper {
    gradleVersion = gradlewVersion
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    group = 'com.mjc.school'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation "org.springframework:spring-core:$springVersion"
        implementation "org.springframework:spring-beans:$springVersion"
        implementation "org.springframework:spring-context:$springVersion"
        implementation "org.springframework:spring-expression:$springVersion"


        implementation "org.springframework:spring-aop:$springVersion"
        implementation "org.springframework:spring-aspects:$springVersion"
        // https://mvnrepository.com/artifact/org.aspectj/aspectjweaver
        implementation 'org.aspectj:aspectjweaver:1.9.24'
        // https://mvnrepository.com/artifact/org.aspectj/aspectjrt
        implementation 'org.aspectj:aspectjrt:1.9.24'


        // https://mvnrepository.com/artifact/javax.annotation/javax.annotation-api
        implementation 'javax.annotation:javax.annotation-api:1.3.2'


        testImplementation "org.springframework:spring-test:$springVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
        testImplementation "com.tngtech.archunit:archunit:$archUnitVersion"

        annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
        implementation "org.mapstruct:mapstruct:$mapstructVersion"
    }
    test {
        useJUnitPlatform()
    }
}

task repack(type: Jar, dependsOn: ':module-main:build') {
    if (findProject(':module-main') != null) {
        entryCompression = ZipEntryCompression.STORED
        def buildJar = zipTree("${project(':module-main').buildDir}/libs/module-main.jar")
        from(buildJar) { into('/') }
        manifest {
            attributes(
                    'Main-Class': 'com.mjc.school.Main'
            )
        }
    }
}